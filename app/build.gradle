apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
        additionalParameters += '--multi-dex'
        additionalParameters += '--set-max-idx-number=35000'//每个包内方法数上限（根据实际项目配置这个数，来适配4.0-4.4一些低端机型，因为拆分的dex太大，这些低端机型加载不了dex）
        additionalParameters += '--minimal-main-dex'
    }
    resourcePrefix "app_"
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", "App")
//                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
        multiDexEnabled true
        ndk {
            //设置支持的SO库架构,其它可以不用。
            abiFilters "armeabi-v7a" //,"armeabi", "arm64-v8a", "x86", "x86_64"
        }
    }
    signingConfigs {
        debug {
            keyAlias "swolf"
            keyPassword "123456"
            storeFile file("../keystore/swolf.jks")
            storePassword "123456"
        }
        release {
            storeFile file("../keystore/swolf.jks")
            storePassword "123456"
            keyAlias "swolf"
            keyPassword "123456"
            v2SigningEnabled false
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true //进行代码混淆处理
            zipAlignEnabled true
            shrinkResources true //移除无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        def date = new Date().format("yyyyMMddHHmmss")
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "FrameTemplateApp_v${defaultConfig.versionName}_${date}_${variant.buildType.name}.apk"
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    kapt rootProject.ext.dependencies.arouter_compiler

    implementation project(path: ':lib_common')
    implementation project(path: ':biz_login')
    implementation project(path: ':biz_main')
    implementation project(path: ':biz_message')
    implementation project(path: ':biz_xml')
}
repositories {
    mavenCentral()
}